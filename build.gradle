apply from: 'dependencies.gradle'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
    }
}

allprojects {
    apply plugin: 'java'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenLocal()
        maven { url 'https://jitpack.io' }
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

var libProjects = [
        project(":jParser:core"),
        project(":jParser:idl"),
        project(":jParser:loader"),
        project(":jParser:cpp"),
        project(":jParser:teavm")
]

configure(libProjects) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = project.groupId

    publishing {
        repositories {
            maven {
                url {
                    def ver = project.version.toString()
                    def isSnapshot = ver.toUpperCase().contains("SNAPSHOT")
                    def repoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    def repoUrlSnapshot = "https://oss.sonatype.org/content/repositories/snapshots/"
                    isSnapshot ? repoUrlSnapshot : repoUrl
                }
                credentials {
                    username = System.getenv('USER')
                    password = System.getenv('PASSWORD')
                }
            }
        }
    }

    javadoc {
        options.encoding = 'UTF-8'
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }


    publishing.publications.configureEach { mav ->
        pom {
            name = "jParser"
            description = "Java JNI code parser"
            url = "http://github.com/xpenatan/jParser"
            developers {
                developer {
                    id = "Xpe"
                    name = "Natan"
                }
            }
            scm {
                connection = "scm:git:git://github.com/xpenatan/jParser.git"
                developerConnection = "scm:git:ssh://github.com/xpenatan/jParser.git"
                url = "http://github.com/xpenatan/jParser/tree/master"
            }
            licenses {
                license {
                    name = "The Apache License, Version 2.0"
                    url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                }
            }
        }
    }

    def signingKey = System.getenv("SIGNING_KEY")
    def signingPassword = System.getenv("SIGNING_PASSWORD")
    if(signingKey != null && signingPassword != null) {
        signing {
            useInMemoryPgpKeys(signingKey, signingPassword)
            publishing.publications.configureEach { pub ->
                sign pub
            }
        }
    }
}